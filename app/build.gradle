apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion buildConfig.compileSdkVer
    buildToolsVersion buildConfig.buildToolsVer

    defaultConfig {
        applicationId 'com.daykm.p5executioner'
        minSdkVersion buildConfig.minSdkVer
        targetSdkVersion buildConfig.targetSdkVer
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        resConfig 'en'
    }

    signingConfigs {
        debug {
            keyAlias 'debugging'
            keyPassword 'debugging'
            storeFile file('debug.jks')
            storePassword 'debugging'
        }
    }

    dataBinding {
        enabled true
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions 'dev'

    productFlavors {
        app {
            dimension 'dev'
        }
    }

    dexOptions {
        // disable predexing in CI
        preDexLibraries = getFlagFromProperties('shouldPreDex')
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

dependencies {
    lib.each { entry -> implementation entry.value }
    proj.each { entry -> implementation(project(entry)) }
    apts.each { entry -> kapt entry.value }
    debugImplementation leakCanary.leakCanary
    releaseImplementation leakCanary.leakCanaryNoOp
    testImplementation leakCanary.leakCanaryNoOp
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation project(path: ':proto')
}
