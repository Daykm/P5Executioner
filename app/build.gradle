apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    compileSdkVersion buildConfig.compileSdkVer
    buildToolsVersion buildConfig.buildToolsVer
    defaultConfig {
        applicationId "com.daykm.p5executioner"
        minSdkVersion buildConfig.minSdkVer
        targetSdkVersion buildConfig.targetSdkVer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resConfig "en"
    }

    buildTypes {
        debug {
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "dev"

    productFlavors {
        app {
            dimension "dev"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
}

ext.ver = [
        anko      : '0.10.1',
        sup       : '26.0.0',
        dagger    : '2.11',
        constraint: '1.0.2',
        timber    : '4.5.1',
        rxJava    : '2.0.8',
        rxAndroid : '2.0.1',
        snapHelper: '1.1',
        moshi     : '1.4.0',
        epoxy     : '2.0.0',
        leakCanary: '1.5',
        callig    : '2.2.0',
        butter    : '8.5.1',
        proto     : '3.0.0'
]

ext.lib = [
        kotlinStd : "org.jetbrains.kotlin:kotlin-stdlib:$buildVer.kotlin",
        ankoSdk15 : "org.jetbrains.anko:anko-sdk21:$ver.anko",
        ankoCompat: "org.jetbrains.anko:anko-appcompat-v7:$ver.anko",
        //ankoCard   : "org.jetbrains.anko:anko-cardview-v7:$ver.anko",
        //ankoRecycle: "org.jetbrains.anko:anko-recyclerview-v7:$ver.anko",
        ankoDesign : "org.jetbrains.anko:anko-design:$ver.anko",
        //TODO wait for this to come out
        // ankoConstraint: "org.jetbrains.anko:anko-constraint:$ver.anko",
        dagger    : "com.google.dagger:dagger:$ver.dagger",
        //supDesign  : "com.android.support:design:$ver.sup",
        constraint: "com.android.support.constraint:constraint-layout:$ver.constraint",
        //cardView   : "com.android.support:cardview-v7:$ver.sup",
        timber    : "com.jakewharton.timber:timber:$ver.timber",
        rxJava    : "io.reactivex.rxjava2:rxjava:$ver.rxJava",
        rxAndroid : "io.reactivex.rxjava2:rxandroid:$ver.rxAndroid",
        epoxy     : "com.airbnb.android:epoxy:$ver.epoxy",
        butter    : "com.jakewharton:butterknife:$ver.butter",
        proto     : "com.google.protobuf:protobuf-lite:$ver.proto"
]

ext.leakCanary = [
        leakCanary    : "com.squareup.leakcanary:leakcanary-android:$ver.leakCanary",
        leakCanaryNoOp: "com.squareup.leakcanary:leakcanary-android-no-op:$ver.leakCanary"
]

ext.apts = [
        dagger: "com.google.dagger:dagger-compiler:$ver.dagger",
        butter: "com.jakewharton:butterknife-compiler:$ver.butter"
]

ext.proj = [':gen', ':proto']

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    lib.each { entry -> implementation entry.value }
    apts.each { entry -> kapt entry.value }
    proj.each { entry -> implementation(project(entry)) }
    debugImplementation leakCanary.leakCanary
    releaseImplementation leakCanary.leakCanaryNoOp
    testImplementation leakCanary.leakCanaryNoOp
    androidTestImplementation project(path: ':proto')
}

